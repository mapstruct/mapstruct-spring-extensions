apply plugin: "maven-publish"
apply plugin: "signing"

dependencies {
    implementation projects.annotations
    implementation libs.javapoet
    implementation libs.commons.lang3

    testImplementation libs.assertj
    testImplementation libs.bundles.junit.jupiter
    testImplementation libs.commons.io
    testImplementation libs.guava
    testImplementation libs.mapstruct.core
    testImplementation libs.mockito
    testImplementation libs.spring.core

    implementation libs.mapstruct.core
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'mapstruct-spring-extensions'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'MapStruct Spring Extensions'
                description = 'Spring-specific MapStruct extensions'
                url = 'https://mapstruct.org/'
                packaging = 'jar'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Raimund Klein'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/mapstruct/mapstruct-spring-extensions.git'
                    developerConnection = 'scm:git:git://github.com/mapstruct/mapstruct-spring-extensions.git'
                    url = 'https://github.com/mapstruct/mapstruct-spring-extensions'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = System.getenv("OSSRH_USERNAME")
                password = System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}