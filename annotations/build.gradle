apply plugin: "maven-publish"
apply plugin: "signing"

dependencies {
    implementation libs.spring.core
    implementation libs.spring.context
    implementation libs.mapstruct.core
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'mapstruct-spring-annotations'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'MapStruct Spring Annotations'
                description = 'Spring-specific annotations for MapStruct'
                url = 'https://mapstruct.org/'
                packaging = 'jar'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'RayBan'
                        name = 'Raimund Klein'
                        email = 'raimund.klein@gmx.de'
                    }
                }
                scm {
                    connection = 'scm:git:git://git@github.com:mapstruct/mapstruct-spring-extensions.git'
                    developerConnection = 'scm:git:ssh://git@github.com:mapstruct/mapstruct-spring-extensions.git'
                    url = 'https://github.com/mapstruct/mapstruct-spring-extensions'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = System.getenv("OSSRH_USERNAME")
                password = System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

javadoc {
    options.links = [
        "https://docs.spring.io/spring/docs/5.0.0.RELEASE/javadoc-api/"
    ]
}