plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
    alias(libs.plugins.vers)
    alias(libs.plugins.spring.boot) apply false
    alias(libs.plugins.spring.dependency.management) apply false
    alias(libs.plugins.asciidoctor) apply false
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { otherIt -> version.toUpperCase().contains(otherIt) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

def specificMinimumVersions = { ModuleComponentIdentifier candidate ->
    candidate.group == "org.mapstruct" || candidate.group == "org.springframework"
}

dependencyUpdates {
    resolutionStrategy {
        componentSelection {
            all {
                if (isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)) {
                    reject('Release candidate')
                } else if (specificMinimumVersions(it.candidate)) {
                    reject('Minimal compatible version')
                }
            }
        }
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "jacoco"

    group = 'org.mapstruct.extensions.spring'
    version = '0.1.2-SNAPSHOT'

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(8))
        }
    }

    test {
        // Use junit platform for unit tests
        useJUnitPlatform()
    }

    jacoco {
        toolVersion = "0.8.5"
    }

    jacocoTestReport {
        reports {
            xml.required = true
            html.required = true
        }
    }

    check.dependsOn jacocoTestReport
}
