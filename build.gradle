plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
    alias(libs.plugins.asciidoctor) apply false
    alias(libs.plugins.dependencycheck)
    alias(libs.plugins.spring.boot) apply false
    alias(libs.plugins.spring.dependency.management) apply false
    alias(libs.plugins.vers)
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { otherIt -> version.toUpperCase().contains(otherIt) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

def specificMinimumVersions = { ModuleComponentIdentifier candidate ->
    candidate.group == "org.mapstruct" || candidate.group == "org.springframework"
}

dependencyUpdates {
    resolutionStrategy {
        componentSelection {
            all {
                if (isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)) {
                    reject('Release candidate')
                } else if (specificMinimumVersions(it.candidate)) {
                    reject('Minimal compatible version')
                }
            }
        }
    }
}

allprojects {
    repositories {
        mavenCentral()
    }

    dependencyCheck {
        analyzers {
            assemblyEnabled = false
            nodeEnabled = false
            nodeAuditEnabled = false
            nugetconfEnabled = false
            nuspecEnabled = false
        }
    }
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "jacoco"

    group = 'org.mapstruct.extensions.spring'
    version = '1.1.2'

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(11))
        }

        withJavadocJar()
        withSourcesJar()
    }

    testing {
        suites {
            test {
                useJUnitJupiter()
            }
        }
    }

    jacoco {
        toolVersion = "0.8.12"
    }

    jacocoTestReport {
        reports {
            xml.required = true
            html.required = true
        }
    }

    check.dependsOn jacocoTestReport
}
